Set Warnings "-notation-overridden".
Set Warnings "-deprecated-hint-without-locality".
Set Warnings "-unexpected-implicit-declaration".

Require Import Category.Lib.
Require Export Category.Theory.Adjunction.
Require Export Category.Theory.Unique.
Require Export Category.Instance.Fun.
Require Import Category.Instance.Cat.

Generalizable All Variables.
Set Primitive Projections.
Set Universe Polymorphism.
Unset Transparent Obligations.

Section KanExtension.

Context {A : Category}.
Context {B : Category}.
Context {F : A ‚ü∂ B}.
Context {C : Category}.

Program Definition Induced : ([B, C]) ‚ü∂ ([A, C]) := {|
  fobj := fun G => G ‚óØ F;
  fmap := fun _ _ f => {| transform := fun z => transform[f] (F z) |}
|}.
Next Obligation. apply naturality. Qed.
Next Obligation. apply naturality_sym. Qed.

Class RightKan := {
  (* jww (2017-06-09): Rename this to ran_functor, RightKan to Ran, and then a
     coercion from Ran to functor? *)
  Ran : [A, C] ‚ü∂ [B, C];

  ran_adjoint : Induced ‚ä£ Ran
}.

Class LocalRightKan (X : A ‚ü∂ C) := {
  LocalRan : B ‚ü∂ C;

  ran_transform : LocalRan ‚óØ F ‚üπ X;

  ump_ran (M : B ‚ü∂ C) (Œº : M ‚óØ F ‚üπ X) :
    @Unique Fun _ _ (fun Œ¥ => Œº ‚âà ran_transform ‚àô Œ¥ ‚ä≤ F)
}.

(* Wikipedia: "There is also a local definition of 'the Kan extension of a
   given functor F along p' which can exist even if the entire functor defined
   above [global Kan extension] does not. This is a generalization of the fact
   that a particular diagram of shape C can have a limit even if not every
   such diagram does. It is also a special case of the fact discussed at
   adjoint functor that an adjoint functor can fail to exist completely, but
   may still be partially defined. If the local Kan extension of every single
   functor exists for some given p : C ‚Üí C' and D, then these local Kan
   extensions fit together to define a functor which is the global Kan
   extension." *)

Global Program Instance RightKan_to_LocalRightKan {R : RightKan} (X : A ‚ü∂ C) :
  LocalRightKan X := {|
  LocalRan := Ran X;
  ran_transform :=
    let adj_from := from (@adj _ _ _ _ ran_adjoint (Ran X) X) nat_id in
    {| transform  := transform[adj_from]
     ; naturality := naturality[adj_from] |}
|}.
Next Obligation.
  srewrite_r (naturality[from (@adj _ _ _ _ ran_adjoint (Ran X) X) nat_id]).
  reflexivity.
Qed.
Next Obligation.
  exists (to (@adj _ _ _ _ (@ran_adjoint R) M X) Œº).
  - intros.
    spose (@from_adj_nat_l _ _ _ _ ran_adjoint) as X0.
    rewrite <- X0; clear X0.
    spose (@iso_from_to _ _ _ (@adj _ _ _ _ ran_adjoint M X) Œº A0) as X0.
    unfold nat_compose; simpl in *.
    rewrites.
    sapply (proper_morphism (@from _ _ _ (@adj _ _ _ _ ran_adjoint M X))).
    simpl; intros; cat.
  - intros.
    assert (Œº ‚âà (adj[ran_adjoint])‚Åª¬π v). {
      intro.
      specialize (X0 A0).
      rewrite X0; clear X0.
      srewrite_r (@from_adj_nat_l _ _ _ _ ran_adjoint).
      destruct (adj[ran_adjoint]); simpl in *.
      destruct from; simpl in *.
      apply proper_morphism; simpl.
      now apply nat_id_left.
    }
    clear -X1.
    destruct (adj[ran_adjoint]); simpl in *.
    intros.
    rewrite <- (iso_to_from v).
    destruct to; simpl in *.
    apply proper_morphism.
    simpl.
    now apply X1.
Qed.

Class LeftKan := {
  Lan : [A, C] ‚ü∂ [B, C];

  lan_adjoint : Lan ‚ä£ Induced
}.

Class LocalLeftKan (X : A ‚ü∂ C) := {
  LocalLan : B ‚ü∂ C;

  lan_transform : X ‚üπ LocalLan ‚óØ F;

  ump_lan (M : B ‚ü∂ C) (Œµ : X ‚üπ M ‚óØ F) :
    @Unique Fun _ _  (fun Œ¥ => Œµ ‚âà Œ¥ ‚ä≤ F ‚àô lan_transform)
}.

Global Program Instance LeftKan_to_LocalLeftKan {R : LeftKan} (X : A ‚ü∂ C) :
  LocalLeftKan X := {|
  LocalLan := Lan X;
  lan_transform :=
    let adj_to := to (@adj _ _ _ _ lan_adjoint X (Lan X)) nat_id in
    {| transform  := transform[adj_to]
     ; naturality := naturality[adj_to] |}
|}.
Next Obligation.
  srewrite_r (naturality[to (@adj _ _ _ _ lan_adjoint X (Lan X)) nat_id]).
  reflexivity.
Qed.
Next Obligation.
  exists (from (@adj _ _ _ _ (@lan_adjoint R) X M) Œµ).
  - intros.
    spose (@to_adj_nat_r _ _ _ _ lan_adjoint) as X0.
    rewrite <- X0; clear X0.
    spose (@iso_to_from _ _ _ (@adj _ _ _ _ lan_adjoint X M) Œµ A0) as X0.
    unfold nat_compose; simpl in *.
    rewrites.
    sapply (proper_morphism (@to _ _ _ (@adj _ _ _ _ lan_adjoint X M))).
    simpl; intros; cat.
  - intros.
    assert (Œµ ‚âà (to adj[lan_adjoint]) v). {
      intro.
      specialize (X0 A0).
      rewrite X0; clear X0.
      srewrite_r (@to_adj_nat_r _ _ _ _ lan_adjoint).
      destruct (to adj[lan_adjoint]); simpl in *.
      apply proper_morphism; simpl.
      now apply nat_id_right.
    }
    clear -X1.
    destruct (adj[lan_adjoint]); simpl in *.
    intros.
    rewrite <- (iso_from_to v).
    destruct from; simpl in *.
    apply proper_morphism.
    simpl.
    now apply X1.
Qed.

End KanExtension.

Arguments RightKan {_ _} F _.
Arguments Ran {_ _} F {_ _}.

Arguments LocalRightKan {_ _} F {_} _.
Arguments LocalRan {_ _} F {_} _ {_}.

Arguments LeftKan {_ _} F _.
Arguments Lan {_ _} F {_ _}.

Arguments LocalLeftKan {_ _} F {_} _.
Arguments LocalLan {_ _} F {_} _ {_}.

Require Import Category.Instance.Fun.

(** From ‚ÄúAll Concepts are Kan Extensions‚Äù, by Marina Christina Lehner:

    "A functor preserves a Kan extension when composing then extending is
    equivalent to extending then composing." *)

Definition preserves_left_Kan `(L : E ‚ü∂ F) :=
  ‚àÄ {C} (G : C ‚ü∂ E) {D} (K : C ‚ü∂ D)
    `{@LeftKan _ _ K E} `{@LeftKan _ _ K F}, L ‚óØ Lan K G ‚âÖ[ [D,F] ] Lan K (L ‚óØ G).

Definition preserves_right_Kan `(R : E ‚ü∂ F) :=
  ‚àÄ {C} (G : C ‚ü∂ E) {D} (K : C ‚ü∂ D)
    `{@RightKan _ _ K E} `{@RightKan _ _ K F}, R ‚óØ Ran K G ‚âÖ[ [D,F] ] Ran K (R ‚óØ G).

(** "We show that left adjoints preserve left Kan extensions, while right
    adjoints will preserve right adjoints [sic]. These connections with
    adjoints run deeper. We will show an adjoint functor theorem which says
    the existence of an adjoint is conditional on a functor having and
    preserving certain Kan extensions." *)

(** jww (2021-08-07): TODO *)
Theorem left_adjoint_impl `(L : C ‚ü∂ D) :
  ‚àÄ R : D ‚ü∂ C, L ‚ä£ R ->
  ‚àÄ {E} (G : E ‚ü∂ C) (H : E ‚ü∂ D),
    [[[E,D]]](L ‚óØ G, H) ‚âä [[[E,C]]](G, R ‚óØ H).
Proof.
  intros.
  simpl.
  isomorphism; simpl.
  - construct.
    + transform.
      * intros.
        apply X; simpl.
        now apply X0.
      * simpl.
        rewrite <- to_adj_nat_l.
        rewrite <- to_adj_nat_r.
        now srewrite (naturality[X0]).
      * simpl.
        rewrite <- to_adj_nat_l.
        rewrite <- to_adj_nat_r.
        now srewrite (naturality[X0]).
    + simpl.
      proper.
      apply to_adj_respects.
      now apply X0.
  - construct.
    + transform.
      * intros.
        apply X; simpl.
        now apply X0.
      * simpl.
        rewrite <- from_adj_nat_l.
        rewrite <- from_adj_nat_r.
        now srewrite (naturality[X0]).
      * simpl.
        rewrite <- from_adj_nat_l.
        rewrite <- from_adj_nat_r.
        now srewrite (naturality[X0]).
    + simpl.
      proper.
      apply from_adj_respects.
      now apply X0.
  - simpl.
    now apply from_adj_comp_law.
  - simpl.
    now apply to_adj_comp_law.
Qed.

Theorem left_adjoints_preserve `(L : C ‚ü∂ D) :
  ‚àÄ R : D ‚ü∂ C, L ‚ä£ R -> preserves_left_Kan L.
Proof.
  intros * is_Adj.  unfold preserves_left_Kan.
  intros ùíû G ùíü K Lan_K_in_C Lan_K_in_D.
  isomorphism.
  - transform.
    + intros ùíπ.
      apply is_Adj; simpl.
      rewrite <- fobj_Compose.
      apply Lan_K_in_C; simpl.
      spose (left_adjoint_impl _ _ is_Adj G (Lan K (L ‚óØ G) ‚óØ K)) as X0.
      transitivity (R ‚óØ (Lan K (L ‚óØ G) ‚óØ K)).
        apply (to X0); simpl.
        apply Lan_K_in_D; simpl.
        exact nat_id.
      now apply fun_comp_assoc.
    + simpl.
      admit.
    + simpl.
      admit.
  - transform.
    + intros ùíπ.
      admit.
    + simpl.
      admit.
    + simpl.
      admit.
  - simpl.
    admit.
  - simpl.
    admit.
Abort.
